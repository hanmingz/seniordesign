**************** Power Discrete MOSFET Electrical Circuit Model *****************
* Product Name: FQP27P06
* 60V P-Channel MOSFET and TO-220
*--------------------------------------------------------------------------------
.SUBCKT FQP27P06 20 10 30
Rg 10 1  1.58
M1 2 1 3 3 DMOS  L=1u  W=1u
.MODEL DMOS PMOS (VTO={-3.10*{-0.00096*TEMP+1.024}} KP={{-0.0068*TEMP}+10.4}
+ THETA=0.0576  VMAX=3.0E5  ETA=0.004  LEVEL=3)
Cgs 1 3 990p
Rd 20 4 0.018  TC=0.0055
Dds 4 3 DDS
.MODEL DDS D(BV={60*{0.000975*TEMP+0.975625}}  M=0.44   CJO=1380p   VJ=0.76)
Dbody 20 3 DBODY
.MODEL DBODY D(IS=3.0E-13  N=1.0  RS=0.036  EG=1.18  TT=105n)
Ra 4 2  0.018  TC=0.0055
Rs 3 5  0.002
Ls 5 30 2.6n
M2 1 8 6 6 INTER
E2 8 6 4 1 2
.MODEL INTER PMOS (VTO=0 KP=10 LEVEL=1)
Cgdmax 7 4 1560p
Rcgd 7 4 1E7
Dgd  4 6 DGD
Rdgd 4 6 1E7
.MODEL DGD D(M=0.42 CJO=1560p VJ=0.24)
M3 7 9 1 1 INTER
E3 9 1 4 1 -2
.ENDS
*-------------------------------------------------------------------------------

**** Power Discrete Thermal Model ****
* 60V P-Channel MOSFET and TO-220
*----------------------------------------------------------------------------
.SUBCKT FQP27P06_THERMAL TH TL
CTHERM1 TH 6 8.8e-4
CTHERM2 6 5  3.1e-3
CTHERM3 5 4  1.8e-2
CTHERM4 4 3  5.6e-2
CTHERM5 3 2  1.8e-1
CTHERM6 2 TL 7.2e-1 

RTHERM1 TH 6 9.4e-3
RTHERM2 6 5  5.1e-2
RTHERM3 5 4  2.2e-1
RTHERM4 4 3  2.5e-1
RTHERM5 3 2  3.2e-1
RTHERM6 2 TL 4.0e-1
.ENDS
*----------------------------------------------------------------------------
* Creation: Nov.-08-2006
* Fairchild Semiconductor

